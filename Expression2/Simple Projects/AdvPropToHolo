@name Initiator
@persist Blueprint:string [Scanner TechEnt ClonEnt Owner]:entity Scan_r [Objects ParentCache IndexesCache BlueprintCache]:table ParentCache_Q Objects_Q CacheMaker CacheLine Indexer Parent_index
@model models/maxofs2d/hover_classic.mdl

#[
    > BASIC INFORMATIONS
      Required "wire_holograms_modelany 1"
      Required "remoteupload" extension
      Github: https://github.com/PawlikKing
    
    > Chat commands:
      /set base        - determines the base location of the scanner by owner currently aimed entity        
      /set clone       - determines e2 chip where the new code will be pasted by owner currently aimed entity                 
      /set r [number]  - determines the radius of the scanning grid             
      /clone            - performs the process
]#

if( first() | dupefinished() )
{
    Owner = owner()
    TechEnt = Scanner = entity()
    holoCreate( 0, Scanner:pos(), vec(0), Scanner:angles(), vec4(0,255,255,125),"models/hunter/misc/sphere375x375.mdl") holoDisableShading( 0, 1 ) holoParent( 0, Scanner ) holoMaterial( 0, "models/shiny" )
    CacheMaker = Indexer = 1
}

runOnChat(1)
    local STR_cut = 
"
"
if( chatClk(Owner) )
{
    local LS = lastSaid()
    
    if( LS:index(1) == "/" )
    {
        hideChat(1)
        local Syntax = LS:explode(" ")
        local Synt = Syntax[1,string]
        
        if( Synt == "/set" )
        {
            local Synt = Syntax[2,string]
            
            if( Synt == "r" )
            {
                Scan_r = Syntax[3,string]:toNumber()  
                holoScaleUnits( 0, vec(Scan_r) )
                Scanner:soundPlay(1,100,"buttons/button24.wav")
            }
            
            if( Synt == "base" )
            {
                TechEnt = Owner:aimEntity()
                if( TechEnt == Scanner )
                { 
                    holoDelete(1)
                    stoptimer( "ScannerLaserPos" ) 
                }
                else
                { 
                    holoCreate( 1, Scanner:pos(), vec(0), Scanner:angles(), vec4(0,255,255,125),"models/hunter/tubes/tube1x1x8.mdl") holoMaterial( 1, "models/shiny" ) holoDisableShading( 1, 1 ) holoParent( 1, Scanner )
                    timer( "ScannerLaserPos", 1 ) 
                }
                holoPos( 0, TechEnt:pos() )
                holoAng( 0, TechEnt:angles() )
                holoParent( 0, TechEnt )
                Scanner:soundPlay(1,100,"buttons/combine_button1.wav")
            }
            
            if( Synt == "clone" )
            {
                ClonEnt = Owner:aimEntity()
                if( ClonEnt == Scanner )
                { 
                    holoDelete(2)
                    stoptimer( "CloneLaserPos" ) 
                }
                else
                { 
                    holoCreate( 2, Scanner:pos(), vec(0), Scanner:angles(), vec4(0,255,0,125),"models/hunter/tubes/tube1x1x8.mdl") holoMaterial( 2, "models/shiny" ) holoDisableShading( 2, 1 ) holoParent( 2, Scanner )
                    timer( "CloneLaserPos", 1 ) 
                }
                Scanner:soundPlay(1,100,"buttons/combine_button1.wav")
            }
            
        }
        
        if( Synt == "/clone" )
        { 

            Blueprint=

"@name Blueprint
@persist Blueprint:table Base:entity I PI

if(first()|dupefinished())
{
    Base = entity()
    Blueprint = table("+STR_cut
            local From = TechEnt:pos()
            findExcludeEntities(array(Scanner,holoEntity(0),holoEntity(1),holoEntity(2)))
            findExcludeModel("models/error.mdl")
            findInSphere(From,Scan_r)
            findSortByDistance(From)
            #findSortByDistance(TechEnt:pos())
            Objects = findToTable()
            Objects_Q = Objects:count()
            timer( "buildCache", 1 )
            Scanner:soundPlay(1,100,"buttons/button6.wav")
        }
        
    }
    
}

local Scanner_pos = Scanner:pos()

if( clk("ScannerLaserPos") )
{
    local TechEnt_pos = TechEnt:pos()
    local Dist = Scanner_pos:distance(TechEnt_pos)
    holoAng(1,(TechEnt_pos-Scanner_pos):toAngle()+ang(90,0,0))
    holoScaleUnits(1,vec(3,3,Dist))
    timer( "ScannerLaserPos", 60 )  
}

if( clk("CloneLaserPos") )
{
    local CloneEnt_pos = ClonEnt:pos()
    local Dist = Scanner_pos:distance(CloneEnt_pos)
    holoAng(2,(CloneEnt_pos-Scanner_pos):toAngle()+ang(90,0,0))
    holoScaleUnits(2,vec(3,3,Dist))
    timer( "CloneLaserPos", 60 )  
}

if( clk("buildCache") )
{
    local Exist = 0
    while( CacheMaker < Objects_Q && perf() )
    {
        local Object = Objects[CacheMaker,entity]
        if(!Object){ CacheMaker++ break }
        local Parent = Object:parent()
        while( CacheLine < ParentCache_Q && perf() )   
        {
            CacheLine++
            local Existing = ParentCache[CacheLine,entity]
            if( Parent == Existing ){ Exist = 1 break }
        }
        
        if( !Exist )
        { 
            if( CacheLine < ParentCache_Q ){ break }
            ParentCache:pushEntity(Parent) 
            ParentCache_Q = ParentCache:count()
        }
        CacheLine = 0 
        Exist = 0
        CacheMaker++  
    }
    if(CacheMaker < Objects_Q){ timer( "buildCache", 60 ) }
    else
    {
         CacheMaker = 0
         timer( "Find", 1 )  
    }
}

if( clk( "Find" ) )
{
    local Exist = 0
    while( CacheMaker < Objects_Q && perf() )
    {
        local Object = Objects[CacheMaker,entity]
        while( CacheLine < ParentCache_Q && perf() )   
        {
            CacheLine++
            local Existing = ParentCache[CacheLine,entity]
            if( Object == Existing ){ Exist = 1 break }
        }
        
        if( Exist )
        { 
            IndexesCache:pushNumber( CacheMaker )  
        }elseif( CacheLine < ParentCache_Q ){ break }
        CacheLine = 0 
        Exist = 0        
        CacheMaker++  
    }
    if(CacheMaker < Objects_Q){ timer( "Find", 60 ) }
    else
    {
         CacheMaker = ParentCache_Q
         timer( "CleanUp", 1 )  
    }  
}

if( clk( "CleanUp" ) )
{
    while( CacheMaker > 0 && perf() )
    {
        Objects:remove(IndexesCache[CacheMaker,number])
        CacheMaker--
    }
    
    if(CacheMaker > 0){ timer( "CleanUp", 60 ) }
    else
    {
         BlueprintCache = ParentCache:add(Objects)
         CacheMaker = 0
         timer( "blueprintCreate", 1 )  
    }
}

if( clk("blueprintCreate") )
{
    local Q = BlueprintCache:count()+1
    while( Indexer < Q && perf() )
    {

        local Object = BlueprintCache[Indexer,entity]
        if(!Object){ Indexer++ break }
        local Parent_F = Object:parent()
        local Await = 1
        if( !Parent_F ){ Parent_index = 0 Await=0 }
        else
        {
            while( Parent_index < Q && perf() )
            {
                Parent_index++
                local Object = BlueprintCache[Parent_index,entity]
                if( Object == Parent_F ){ Await=0 break }
            }
            if( Parent_index >= Q ){ Await=0 }
        }

        if( Await )
        {break}
        else 
        {
            local Index    = Indexer:toString()
            local Parent   = Parent_index:toString()
            local Position = round(Parent_F:toLocal(Object:pos()),3)
            local Angle    = round(Parent_F:toLocal(Object:angles()),3)
            local Scale    = round(Object:boxSize(),3)
            local Model    = Object:model()
            local Material = Object:getMaterial()
            local Color4   = Object:getColor4()
            
            local STR_Position = "vec( "+Position:x():toString()+", "+Position:y():toString()+", "+Position:z():toString()+" )"
            local STR_Angle = "ang( "+Angle:pitch():toString()+", "+Angle:yaw():toString()+", "+Angle:roll():toString()+" )"
            local STR_Scale = "vec( "+Scale:x():toString()+", "+Scale:y():toString()+", "+Scale:z():toString()+" )"
            local STR_Model = "\""+Model+"\""
            local STR_Material = "\""+Material+"\""
            local STR_Color4 = "vec4( "+Color4[1]:toString()+", "+Color4[2]:toString()+", "+Color4[3]:toString()+", "+Color4[4]:toString()+" )"
            
            Indexer++
            Ct=","+STR_cut
            if(Indexer==Q){ Ct=STR_cut+"    )" }
            
            local DataBlock = "        table( "+Index+", "+Parent+", "+STR_Position+", "+STR_Angle+", "+STR_Scale+", "+STR_Model+", "+STR_Material+", "+STR_Color4+" )"+Ct
            Blueprint = Blueprint + DataBlock
            Parent_index = 0
        }
    }
    if( Indexer < Q ){ timer( "blueprintCreate", 60 ) }
    else
    { 
        Indexer = 0
        Blueprint = Blueprint+STR_cut+"    timer( \"assemble\", 1 )"+STR_cut+"}"+STR_cut+
"
if( clk(\"assemble\") )
{
    local Q = Blueprint:count()
    while( I < Q && perf() && holoCanCreate() )
    {
        I++
        HoloData = Blueprint[I,table]
        local Index    = HoloData[1,number]
        local Parent = HoloData[2,number]
        local Par = Base
        if( Parent )
        {
            Par = holoEntity(Parent)            
        }
        else
        {
            Par = HoloData[2,entity]  
            if(!Par){Par=Base}  
        }
        local Position = Par:toWorld(HoloData[3,vector])
        local Angle    = Par:toWorld(HoloData[4,angle])
        local Model    = HoloData[6,string]
        local Scale    = HoloData[5,vector]
        local Material = HoloData[7,string]
        local Color4   = HoloData[8,vector4]
        holoCreate(Index,Position,vec(),Angle,Color4,Model)
        holoParent(Index,Par)
        holoMaterial(Index,Material)
        holoScaleUnits(Index,Scale)
    }
    if( I < Q )
    { timer( \"assemble\", 250 ) }
}"
        ClonEnt:remoteSetCode(Blueprint)
        stopAllTimers()
        CacheMaker = Indexer = 1
        Objects:clear() 
        ParentCache:clear()  
        IndexesCache:clear()  
        BlueprintCache:clear() 
        ParentCache_Q = Objects_Q = CacheLine = Parent_index = 0
        Blueprint = ""
    }
}
